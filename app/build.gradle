import org.gradle.internal.os.OperatingSystem;

plugins {
    id 'application'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.2'
}

application {
    mainModule = 'jlink.app'
    mainClass = 'com.github.srcmaxim.jlink.App'
}

tasks.named('test') {
    useJUnitPlatform()
}

task jlink(type:Exec) {
    dependsOn tasks.build
    workingDir "$buildDir"

    def name = project.getName()
    def mainModule = application.mainModule.get()
    def mainClass = application.mainClass.get()
    def command = """jlink \
        --verbose \
        --compress 2 \
        --strip-debug \
        --no-header-files \
        --no-man-pages \
        --module-path libs/${name}.jar \
        --add-modules ${mainModule} \
        --launcher app=${mainModule}/${mainClass} \
        --output jre
        """

    command = OperatingSystem.current().isWindows()
        ? ['cmd', '/c', command] : ['/bin/sh', '-c', command]
    commandLine(command)
}